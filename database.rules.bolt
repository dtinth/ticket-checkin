path /events/{eventId} {
  read() { isEventAdmin(eventId) }
  path /info {
    write() { isEventAdmin(eventId) }
  }
  path /keys is Keys {}
  path /admins/{userId} is Boolean {
    read() { auth.uid === userId }
  }
  path /attendees/{attendeeId} is Attendee {
    read() { isStaff(eventId) }
  }
  path /checkins/{attendeeId} is CheckIn {
    read() { isStaff(eventId) }
  }
  path /staffs/{staffId} is Staff {
    read() { isStaff(eventId) }
  }
}

type Keys {
  attendee: String,
  staff: String
}

type Staff {
  // If available for fulfillment, then put a unique ID here
  available: String | Null,

  // Filfillment jobs
  jobs: Map<String, FulfillmentJob>
}

type Attendee {
  // Display name
  displayName: String,

  // Extra information
  info: Any | Null
}

type FulfillmentJob {
  time: Number,
  refCode: String,
  displayName: String,
  info: Any | Null
}

type CheckIn {
  time: Number,
  mode: String
}

isEventAdmin(eventId) { root.events[eventId].admins[auth.uid] != null }
isStaff(eventId) { false }
